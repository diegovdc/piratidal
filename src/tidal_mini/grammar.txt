(* Basic types *)
ws = #"\s+"
word = #"[a-zA-Z]+"
int = #"[0-9]+"
float = #"[+-]?([0-9]*[.])?[0-9]+"
silence = "~"
<token> = word | sample | int | silence | fast | group | alt | slow | polymeter | elongate | replicate | euclidean | choose | degrade

(* Pattern *)
cat = token (<ws>? token)*
pattern = cat (<ws> <'.'> <ws> cat)*

(* Pattern structures *)
stack = pattern (<ws> <','> <ws> pattern)*
group = <#'\['> stack <#'\]'>
alt = <#'\<'> stack <#'\>'>
polymeter = <#'\{'> stack <#'\}'> polymeter-steps?
polymeter-steps = <'%'> int
op-choose = <ws '|' ws>
choose = <#'\['> stack (<op-choose> stack)+ <#'\]'>

(* Pattern Operations *)
op-replicate =  <'!'> int
replicate = token op-replicate
op-elongate =  <'@'> int
elongate = token op-elongate
op-sample =  <':'> int
sample = word op-sample
op-euclidean = <'('> int <ws? ',' ws?> int (<ws? ',' ws?> int)? <')'>
euclidean = (word | int | float | sample) op-euclidean
op-fast = <'*'> int
fast = token op-fast
op-slow = <'/'> int
slow = token op-slow
op-degrade = <'?'> float?
degrade = token op-degrade
